apply from: '../banks.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.owasp:dependency-check-gradle:${property('owasp.dependencyCheck.version')}"
    }
}

apply plugin: "com.android.application"
apply plugin: "org.owasp.dependencycheck"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.payneteasy.dengisend"
        versionCode 1
        versionName "1.0"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".mock"
            buildConfigField "String", "PAYNET_BASE_URL", "\"https://sandbox.payneteasy.com/paynet/dapi/v1\""
            buildConfigField "String", "MERCHANT_BASE_URL", "\"https://dengisend-transfer-ios.pne.io\""
            versionNameSuffix = "-${getCurrentDate()}"
        }
        release {
            minifyEnabled false
        }
    }

    flavorDimensions 'tier'

    // If you need to add more flavors, consider using flavor dimensions.
    // flavor name = bank
    productFlavors {
        dengisend {
            applicationIdSuffix = ".dengisend"
        }
    }
    buildToolsVersion '28.0.3'
}

dependencyCheck {
    format = "${property('owasp.dependencyCheck.format')}"
}

android.applicationVariants.all { variant ->
    variant.buildConfigField "String", "BANK_ID", "\"" + banksMap[variant.productFlavors.get(0).name] + "\""
}

dependencies {
    implementation project(':dengisend')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$rootProject.supportLibraryVersion"
    implementation "com.google.guava:guava:$rootProject.guavaVersion"

    // card.io
    implementation "io.card:android-sdk:$rootProject.cardioVersion"

    // Dagger dependencies
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"

    // Espresso UI Testing
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"

    // Resolve conflicts between main and test APK:
    androidTestImplementation "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:design:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
}

def static getCurrentDate() {
    return new Date().format('yyyyMMddHHmm')
}
